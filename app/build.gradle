/*
 * Copyright 2017 75py
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.cookpad.android.licensetools'
apply plugin: 'project-report'
apply plugin: 'jacoco-android'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm:ss'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 25
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "com.nagopy.android.callconfirm"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode publish_version_code
        versionName "$publish_version_name ($publish_version_code, $gitSha, $buildTime)"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/rxjava.properties'
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
        unitTests.all {
            maxHeapSize = '2048m'
            jvmArgs '-XX:MaxPermSize=512m', '-XX:-UseSplitVerifier'
        }
    }
}
jacoco {
    toolVersion = "0.7.2+"
}
jacocoAndroidUnitTestReport {
    excludes += [
            '**/*_MembersInjector.class',
            '**/Dagger*Component.class',
            '**/Dagger*Component$Builder.class',
            '**/*Module_*Factory.class',
            '**/*_Factory.class',
            '**/R.class',
            '**/R$*.class',
            '**/DataBindingInfo.class',
            '**/android/databinding/*.*',
            '**/com/android/**/*.*',
            'android/**/*.*',
            '**/BR.class',
            'com/nagopy/android/callconfirm/databinding/*.*',
            'com/nagopy/android/callconfirm/**/*PermissionsDispatcher.class',
            'com/nagopy/android/callconfirm/di/*'
    ]
    csv.enabled false
    html.enabled true
    xml.enabled true
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.4.0'
    compile 'com.android.support:support-v13:25.4.0'
    compile 'com.android.support:percent:25.4.0'
    compile 'com.android.support:customtabs:25.4.0'
    compile 'com.jakewharton.timber:timber:4.6.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.5'
    compile 'com.trello.rxlifecycle2:rxlifecycle:2.1.0'
    compile 'com.trello.rxlifecycle2:rxlifecycle-components:2.1.0'
    compile 'com.google.dagger:dagger:2.12'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.12'
    compile 'com.github.hotchemi:permissionsdispatcher:2.4.0'
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:2.4.0'
    annotationProcessor 'com.android.databinding:compiler:3.0.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.11.0'
    testCompile "com.google.truth:truth:0.31"
    testCompile "org.robolectric:robolectric:3.5.1"

    // https://github.com/robolectric/robolectric/issues/1932
    testCompile 'org.khronos:opengl-api:gl1.1-android-2.1_r1'
}
